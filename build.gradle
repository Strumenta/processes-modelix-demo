buildscript {
    repositories {
        jcenter()
    }  
}


repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://dl.bintray.com/modelixorg/maven/' }
    maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
}

configurations {
    mpsArtifacts
    apps
}

ext.artifactsDir = new File(rootDir, 'artifacts')
ext.appsDir = new File(rootDir, 'apps')

dependencies {
    mpsArtifacts "org.modelix:mps-model-plugin:0.0.6"
    mpsArtifacts "de.itemis.mps:extensions:2020.1+"
    apps "org.modelix:model-server-fatjar:0.0.6"
}

task resolveMpsArtifacts(type: Copy) {
    dependsOn configurations.mpsArtifacts
    from {
        configurations.mpsArtifacts.resolve().collect { zipTree(it) }
    }
    into artifactsDir
    doFirst { delete artifactsDir }
}

task resolveApps(type: Copy) {
    dependsOn configurations.apps
    from {
        configurations.apps.resolve()
    }
    into appsDir
    doFirst { delete appsDir }
}

task setup(dependsOn: [resolveMpsArtifacts, resolveApps]) {
    doLast {
        print "Setup completed."
    }
}
